plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keyStorePassword = System.getenv('KEYSTORE_PASSWORD')
def keyAvailable = !!keyStorePassword

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "tech.httptoolkit.pinning_demo"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 6
        versionName "1.3.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("./keystore.jks")
            storePassword keyStorePassword
            keyAlias "signing_key"
            keyPassword keyStorePassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            if (keyAvailable) {
                signingConfig signingConfigs.release
            } else {
                logger.warn('No signing key available')
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.squareup.okhttp3:okhttp:4.5.0'
    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.datatheorem.android.trustkit:trustkit:1.1.3'
    implementation 'androidx.preference:preference-ktx:1.1.1'
}